table _measures
	lineageTag: fe3edaca-7c07-4f27-99c3-2ddf28ba8f61

	measure 'Total Orders' = ```
			DISTINCTCOUNT ( f_orders[order_id] )
			
			
			```
		formatString: #,0
		displayFolder: Sales
		lineageTag: 32d97663-b238-4aee-86b3-073c0446c0fd

		changedProperty = IsHidden

	measure 'Total Revenue' = ```
			SUM ( f_order_items[price] )
			
			```
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: Sales
		lineageTag: b6d994bf-34da-47b5-bf97-ad19f44edefc

		changedProperty = IsHidden

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Total Freight' = ```
			SUM ( f_order_items[freight_value] )
			
			```
		formatString: \$#,0;(\$#,0);\$#,0
		displayFolder: Sales
		lineageTag: a7c151a6-4fcf-4bca-aa76-01935e2898fb

		changedProperty = IsHidden

	measure AOV = ```
			DIVIDE ( [Total Revenue], [Total Orders] )
			
			```
		formatString: \$#,0.0;(\$#,0.0);\$#,0.0
		displayFolder: Sales
		lineageTag: 4e2765a9-ce2d-427d-a955-e988b9843902

		changedProperty = IsHidden

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Delivered Orders' = ```
			
			CALCULATE (
			    DISTINCTCOUNT ( f_orders[order_id] ),
			    f_orders[order_status] = "delivered"
			)
			
			```
		formatString: #,0
		displayFolder: Delivery
		lineageTag: e3aa53a8-239a-442a-975b-a70ff7c2a4af

		changedProperty = IsHidden

	measure 'Avg Delivery Days' = ```
			
			AVERAGEX (
			    FILTER (
			        f_orders,
			        f_orders[order_status] = "delivered"
			            && NOT ISBLANK ( f_orders[order_delivered_customer_date] )
			            && NOT ISBLANK ( f_orders[order_purchase_timestamp] )
			    ),
			    DATEDIFF (
			        f_orders[order_purchase_timestamp],
			        f_orders[order_delivered_customer_date],
			        DAY
			    )
			)
			
			```
		formatString: 0
		displayFolder: Delivery
		lineageTag: bda32d8b-fe7f-4ed6-a690-be7436825b4c

		changedProperty = IsHidden

	measure 'On-Time Delivery %' = ```
			
			VAR Delivered =
			    FILTER (
			        f_orders,
			        f_orders[order_status] = "delivered"
			            && NOT ISBLANK ( f_orders[order_delivered_customer_date] )
			            && NOT ISBLANK ( f_orders[order_estimated_delivery_date] )
			    )
			VAR OnTimeCount =
			    COUNTROWS (
			        FILTER (
			            Delivered,
			            f_orders[order_delivered_customer_date] <= f_orders[order_estimated_delivery_date]
			        )
			    )
			VAR DeliveredCount =
			    COUNTROWS ( Delivered )
			RETURN
			DIVIDE ( OnTimeCount, DeliveredCount )
			
			```
		formatString: 0\ %;-0\ %;0\ %
		displayFolder: Delivery
		lineageTag: 76034799-d6cc-4bb0-aeb9-788f96879461

		changedProperty = IsHidden

	measure 'Total Reviews' = ```
			
			COUNTROWS ( f_reviews )
			
			```
		formatString: #,0
		displayFolder: Reviews\Core
		lineageTag: 7684a3aa-6990-4756-99d1-852b809d9068

		changedProperty = IsHidden

	measure 'Avg Review Score' = ```
			
			AVERAGE ( f_reviews[review_score] )
			
			```
		formatString: 0.00
		displayFolder: Reviews\Core
		lineageTag: 38329cda-cc9a-479f-8a6e-f08d6cc0999b

		changedProperty = IsHidden

	measure 'Low Reviews %' = ```
			
			DIVIDE (
			    CALCULATE ( COUNTROWS ( f_reviews ), f_reviews[review_score] <= 2 ),
			    [Total Reviews]
			)
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Reviews\Core
		lineageTag: d21fc38d-f4ed-489d-9a9e-de8ca4f68767

		changedProperty = IsHidden

	measure 'Delivered Orders (SLA)' = ```
			
			CALCULATE (
			    DISTINCTCOUNT ( f_orders[order_id] ),
			    f_orders[order_status] = "delivered",
			    NOT ISBLANK ( f_orders[delivery_sla_bucket] )
			)
			
			```
		formatString: 0
		displayFolder: Delivery
		lineageTag: d5294356-57d7-4fa9-bb63-50818926a49d

	measure 'Delivered Orders (for CDF)' = ```
			
			CALCULATE (
			    DISTINCTCOUNT ( f_orders[order_id] ),
			    f_orders[order_status] = "delivered",
			    NOT ISBLANK ( f_orders[delivery_days] )
			)
			
			```
		formatString: 0
		displayFolder: Delivery\CDF
		lineageTag: 36c1e4e8-ba45-4663-9681-913ca8a94bc6

	measure 'Delivered Orders ≤ Day' = ```
			
			VAR MaxDay = MAX ( d_delivery_days[DeliveryDay] )
			RETURN
			CALCULATE (
			    DISTINCTCOUNT ( f_orders[order_id] ),
			    f_orders[order_status] = "delivered",
			    NOT ISBLANK ( f_orders[delivery_days] ),
			    f_orders[delivery_days] <= MaxDay
			)
			
			```
		formatString: 0
		displayFolder: Delivery
		lineageTag: 95593bfe-d7fe-4368-b922-83d0bf51a8b2

	measure 'Delivery CDF %' = ```
			
			DIVIDE ( [Delivered Orders ≤ Day], [Delivered Orders (for CDF)] )
			
			```
		formatString: 0\ %;-0\ %;0\ %
		displayFolder: Delivery\CDF
		lineageTag: ee8be3d9-591e-44c1-8bcb-c008e30b0b72

	measure 'Delivered ≤ 7d %' = ```
			
			VAR sla = 7
			RETURN
			DIVIDE (
			    CALCULATE (
			        DISTINCTCOUNT ( f_orders[order_id] ),
			        f_orders[order_status] = "delivered",
			        NOT ISBLANK ( f_orders[delivery_days] ),
			        f_orders[delivery_days] <= sla
			    ),
			    [Delivered Orders (for CDF)]
			)
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Delivery\CDF
		lineageTag: 64ad8d1f-ecf7-48b1-be65-e2a5801a9a80

	measure 'Delivered ≤ 15d %' = ```
			
			VAR sla = 15
			RETURN
			DIVIDE (
			    CALCULATE (
			        DISTINCTCOUNT ( f_orders[order_id] ),
			        f_orders[order_status] = "delivered",
			        NOT ISBLANK ( f_orders[delivery_days] ),
			        f_orders[delivery_days] <= sla
			    ),
			    [Delivered Orders (for CDF)]
			)
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Delivery\CDF
		lineageTag: 3a4e1c01-3193-4c8d-afa0-31960b85ee17

	measure 'Delivered ≤ 30d %' = ```
			
			VAR sla = 30
			RETURN
			DIVIDE (
			    CALCULATE (
			        DISTINCTCOUNT ( f_orders[order_id] ),
			        f_orders[order_status] = "delivered",
			        NOT ISBLANK ( f_orders[delivery_days] ),
			        f_orders[delivery_days] <= sla
			    ),
			    [Delivered Orders (for CDF)]
			)
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Delivery\CDF
		lineageTag: 9f4d1adb-ee57-4cd0-b9a1-bb738562ab37

	measure 'Delivered > 30d %' = ```
			
			VAR sla = 30
			RETURN
			DIVIDE (
			    CALCULATE (
			        DISTINCTCOUNT ( f_orders[order_id] ),
			        f_orders[order_status] = "delivered",
			        NOT ISBLANK ( f_orders[delivery_days] ),
			        f_orders[delivery_days] > sla
			    ),
			    [Delivered Orders (for CDF)]
			)
			
			```
		formatString: 0.00\ %;-0.00\ %;0.00\ %
		displayFolder: Delivery\CDF
		lineageTag: 2020153b-2b80-4268-8f13-a6dc9c5f982e

	measure 'Positive Reviews' = ```
			
			CALCULATE ( [Total Reviews], f_reviews[review_score] IN { 4, 5 } )
			
			```
		formatString: 0
		displayFolder: Reviews\Core
		lineageTag: ded0dbfa-f886-47cc-a369-6f16ff066c85

	measure 'Positive Reviews %' = ```
			
			DIVIDE ( [Positive Reviews], [Total Reviews] )
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Reviews\Core
		lineageTag: 1a403037-3f20-4ffc-9465-2fe8b365adfd

	measure 'Negative Reviews' =
			
			CALCULATE ( [Total Reviews], f_reviews[review_score] IN { 1, 2 } )
		formatString: 0
		displayFolder: Reviews\Core
		lineageTag: 6949b24f-8600-4d34-8dae-c8337ab0101f

	measure 'Negative Reviews %' = ```
			
			DIVIDE ( [Negative Reviews], [Total Reviews] )
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Reviews\Core
		lineageTag: 4bba8bf0-9efa-42d9-aafc-33f711b1bee7

	measure 'Avg Review (≤ 30d)' = ```
			
			CALCULATE (
			    [Avg Review Score],
			    FILTER (
			        f_orders,
			        NOT ISBLANK ( f_orders[delivery_days] )
			            && f_orders[delivery_days] <= 30
			    )
			)
			
			```
		displayFolder: Reviews\Delivery Impact
		lineageTag: fb3c2900-935a-450a-82da-b43b252f7260

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Avg Review (> 30d)' = ```
			
			CALCULATE (
			    [Avg Review Score],
			    FILTER (
			        f_orders,
			        NOT ISBLANK ( f_orders[delivery_days] )
			            && f_orders[delivery_days] > 30
			    )
			)
			
			
			```
		displayFolder: Reviews\Delivery Impact
		lineageTag: 303c5944-584a-4245-a221-25f970e78553

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Review Gap (>30d vs ≤30d)' = ```
			
			[Avg Review (> 30d)] - [Avg Review (≤ 30d)]
			
			```
		displayFolder: Reviews\Delivery Impact
		lineageTag: 13086717-6a7e-4661-aedb-15410c34ec9f

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Avg Delivery Days (Reviewed)' = ```
			
			AVERAGEX (
			    VALUES ( f_reviews[order_id] ),
			    CALCULATE ( MAX ( f_orders[delivery_days] ) )
			)
			
			```
		displayFolder: Reviews\Helper
		lineageTag: 37abd756-486c-458a-91d7-42b67be73ad2

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Neutral Reviews' = ```
			
			CALCULATE ( [Total Reviews], f_reviews[review_score] = 3 )
			
			```
		formatString: #,0
		displayFolder: Reviews\Core
		lineageTag: f94cafc0-5504-48dc-96f4-63c96139f325

	measure 'Neutral Reviews %' = ```
			
			DIVIDE ( [Neutral Reviews], [Total Reviews] )
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Reviews\Core
		lineageTag: fba313e3-51e2-40a0-9897-ff6036c2ae36

	measure 'Reviews Mix Check %' = ```
			
			[Positive Reviews %] + [Neutral Reviews %] + [Negative Reviews %]
			
			```
		formatString: 0.0\ %;-0.0\ %;0.0\ %
		displayFolder: Reviews\Core
		lineageTag: 4c754bff-8148-4bcc-b91f-32572e86e385

	measure 'Positive Reviews (Count)' = [Positive Reviews]
		formatString: #,0
		displayFolder: Reviews\Core
		lineageTag: ca9b1f11-47dc-4f22-9797-1f9723335adb

	measure 'Neutral Reviews (Count)' = [Neutral Reviews]
		formatString: #,0
		displayFolder: Reviews\Core
		lineageTag: 51f1ae33-0125-4502-98b6-cb91d957ee79

	measure 'Negative Reviews (Count)' = [Negative Reviews]
		formatString: #,0
		displayFolder: Reviews\Core
		lineageTag: 64246fe6-54a0-4be3-86f2-dfbf4d48fe32

	measure 'Reviews by Type' = ```
			
			VAR rt = SELECTEDVALUE ( d_review_type[Review Type] )
			RETURN
			SWITCH (
			    rt,
			    "Negative (1–2)", CALCULATE ( [Total Reviews], f_reviews[review_score] IN { 1, 2 } ),
			    "Neutral (3)",    CALCULATE ( [Total Reviews], f_reviews[review_score] = 3 ),
			    "Positive (4–5)", CALCULATE ( [Total Reviews], f_reviews[review_score] IN { 4, 5 } ),
			    BLANK()
			)
			
			```
		formatString: 0
		displayFolder: Reviews\Delivery Impact
		lineageTag: fac91068-246c-4933-9189-5f8f846b781e

	partition _measures = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i45WqlCKjQUA", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [MeasureTable = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"MeasureTable", type text}}),
				    #"Removed Columns" = Table.RemoveColumns(#"Changed Type",{"MeasureTable"})
				in
				    #"Removed Columns"

	changedProperty = IsHidden

	annotation PBI_ResultType = Table

